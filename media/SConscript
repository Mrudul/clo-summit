#!/usr/bin/env python
# -*- coding: utf-8 -*-

Import('env')

from util import match_patterns, minifiables, filter_files
from fnmatch import fnmatch
from os.path import basename
from filesystem import walk_dir

SRC_DIR = './'
DEST_DIR = '../' + env['MEDIA_DEST_DIR'] + '/'

SCRIPT_FILES = Split("""
    script/lib/chickoojs/src/jquery/jquery-1.3.2.js
    script/lib/chickoojs/src/jquery/form/jquery.form.js
    script/lib/chickoojs/src/jquery/metadata/jquery.metadata.js
    script/lib/chickoojs/src/jquery/validate/jquery.validate.js
    script/lib/chickoojs/src/jquery/validate/jquery.validate.extra.js
    script/lib/chickoojs/src/jquery/alphanumeric/jquery.alphanumeric.js

    script/register.js
    script/speaker.js
    script/survey_participate.js
    script/forms.js
    script/index.js
    script/qoutes.js
    script/jquery.innerfade.js
""")

IMAGE_FILES = Split("""
    image/background/clo_block.png
    image/background/logo.png
    image/logo/tiss.png
    image/logo/leapvault.png
    image/background/individual_pricing.png
    image/background/group_of_3_pricing.png
    image/background/group_of_6_pricing.png
    image/icon/favicon.png
    image/background/register.png
    image/background/survey.png
    image/background/partners.png
    image/background/textfield_normal.png
    image/background/textfield_readonly.png
    image/background/button_overlay.png
    image/background/pixels.jpg
    image/logo/facebook.png
    image/logo/twitter.png
    image/logo/linkedin.png
    image/background/twitter.png
""")

CSS_FILES = Split("""
    css/screen.css
    css/generic.css
    css/print.css
""")

# Do not include IMAGE files in here.
# Please include them in IMAGE_FILES above.
# If you would like images to be copied "as is" without
# any optimizations, then you may include them here.
PLAIN_COPIES = SCRIPT_FILES + CSS_FILES + Split("""
    robots.txt
    downloads/registration_form.pdf
""")

PLAIN_COPY_DIRECTORIES = Split("""

""")

# Recursively add all the files in the directories to files to copy.
for directory in set(PLAIN_COPY_DIRECTORIES):
    files = [f[len(SRC_DIR):] for f in walk_dir(SRC_DIR + directory)]
    PLAIN_COPIES.extend(files)

# Plain copies.
for f in set(PLAIN_COPIES):
    env.Command(DEST_DIR + f, SRC_DIR + f, Copy("$TARGET", "$SOURCE"))

# Optimize graphics
for f in IMAGE_FILES:
    filename = str(f)
    if match_patterns(filename, patterns=['*.png']):
        env.OptimizePNG(DEST_DIR + f, SRC_DIR + f)
    elif match_patterns(filename, patterns=['*.jpg', '*.jpeg']):
        env.OptimizeJPEG(DEST_DIR + f, SRC_DIR + f)
    else:
        env.Command(DEST_DIR + f, SRC_DIR + f, Copy("$TARGET", "$SOURCE"))


# Stitching graphics together
stitched_images = [
        dict(
            target='image/button/nav.png',
            sources=Split("""
                image/button/register.png
                image/button/nominate_speaker.png
            """),
            orientation='horizontal'  # or "vertical"
            ),
]
for stitch in stitched_images:
    func = env.HStitchImages if stitch.get('orientation', 'horizontal') == 'horizontal' else env.VStitchImages
    func(DEST_DIR + stitch['target'], [SRC_DIR + f for f in stitch['sources']])


# Minification of the generated CSS and JS files.
MINIFIABLE_FILES = SCRIPT_FILES + CSS_FILES + Split("""

""")

# Exclude these files specifically from minification.
NON_MINIFIABLE_FILES = Split("""

""")


# Don't minify all files in one command.  Minify them separately.
# It keeps build times small.
for f in minifiables(MINIFIABLE_FILES, NON_MINIFIABLE_FILES):
    filepath = DEST_DIR + str(f)
    env.YuiCompressorMinify(filepath)
    #if fnmatch(basename(filepath), '*.js'):
    #    env.JsminMinify(filepath)

# Combine these JavaScript files and create a common js.
#
# Warning:  THE ORDER MATTERS.
#
LIB_INDEX_JS_FILES =  Split("""
    script/jquery.innerfade.js
    script/lib/chickoojs/src/jquery/tools/1.1.1/tools.flashembed-1.0.4.js
    script/lib/chickoojs/src/jquery/tweet/jquery.tweet.js
    script/index.js
""")
LIB_INDEX_JS_FILES = [SRC_DIR + f for f in LIB_INDEX_JS_FILES]
lib_index_js = env.Concatenate([DEST_DIR + 'script/page/index.js'], LIB_INDEX_JS_FILES)
env.YuiCompressorMinify(lib_index_js)
env.JsminMinify(lib_index_js)

screen_all_css_files =  Split("""
    css/generic.css
    css/screen.css
    """)
screen_all_css_files = [SRC_DIR + f for f in screen_all_css_files]
screen_all_css = env.Concatenate([DEST_DIR + 'css/screen-all.css'], screen_all_css_files)
env.YuiCompressorMinify(screen_all_css)

SURVEY_PARTICIPATE_JS_FILES =  Split("""
    script/lib/chickoojs/src/jquery/form/jquery.form.js
    script/lib/chickoojs/src/jquery/metadata/jquery.metadata.js
    script/lib/chickoojs/src/jquery/validate/jquery.validate.js
    script/lib/chickoojs/src/jquery/validate/jquery.validate.extra.js
    script/lib/chickoojs/src/jquery/alphanumeric/jquery.alphanumeric.js
    script/forms.js
    script/survey_participate.js
""")
SURVEY_PARTICIPATE_JS_FILES = [SRC_DIR + f for f in SURVEY_PARTICIPATE_JS_FILES]
survey_participate_js = env.Concatenate([DEST_DIR + 'script/page/survey_participate.js'], SURVEY_PARTICIPATE_JS_FILES)
env.YuiCompressorMinify(survey_participate_js)
#env.JsminMinify(survey_participate_js)


REGISTER_JS_FILES =  Split("""
    script/lib/chickoojs/src/jquery/form/jquery.form.js
    script/lib/chickoojs/src/jquery/metadata/jquery.metadata.js
    script/lib/chickoojs/src/jquery/validate/jquery.validate.js
    script/lib/chickoojs/src/jquery/validate/jquery.validate.extra.js
    script/lib/chickoojs/src/jquery/alphanumeric/jquery.alphanumeric.js
    script/lib/chickoojs/src/jquery/defer/jquery.defer.js
    script/forms.js
    script/register.js
""")
REGISTER_JS_FILES = [SRC_DIR + f for f in REGISTER_JS_FILES]
register_js = env.Concatenate([DEST_DIR + 'script/page/register.js'], REGISTER_JS_FILES)
env.YuiCompressorMinify(register_js)
#env.JsminMinify(register_js)

SPEAKER_JS_FILES =  Split("""
    script/lib/chickoojs/src/jquery/form/jquery.form.js
    script/lib/chickoojs/src/jquery/metadata/jquery.metadata.js
    script/lib/chickoojs/src/jquery/validate/jquery.validate.js
    script/lib/chickoojs/src/jquery/validate/jquery.validate.extra.js
    script/lib/chickoojs/src/jquery/alphanumeric/jquery.alphanumeric.js
    script/forms.js
    script/speaker.js
""")
SPEAKER_JS_FILES = [SRC_DIR + f for f in SPEAKER_JS_FILES]
speaker_js = env.Concatenate([DEST_DIR + 'script/page/speaker.js'], SPEAKER_JS_FILES)
env.YuiCompressorMinify(speaker_js)
#env.JsminMinify(speaker_js)



# Include licenses.
#c3 = env.Concatenate([DEST_DIR + 'script/lib/all-min.js'], [
#    DEST_DIR + 'script/LICENSES',
#    DEST_DIR + 'script/lib/all_o-min.js',
#])

